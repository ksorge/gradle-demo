/*
 *  Copyright 2014 LiveVox, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
buildscript {
    repositories {
        maven {
            url "${HOST}/plugins-releases"
            credentials {
                username = "${USER}"
                password = "${PASSWORD}"
            }
        }
    }
    dependencies {
        classpath "com.github.townsfolk:gradle-release:1.2"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'release'

configurations { deployerJars }

group="com.livevox.demo"
description="LiveVox Gradle Project Demo"
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    maven {
        url "${HOST}/repo"
        credentials {username = "${USER}"; password = "${PASSWORD}" }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "${HOST}/libs-releases-local") {
            authentication(userName: "${USER}", password: "${PASSWORD}")
        }
        snapshotRepository(url: "${HOST}/libs-snapshots-local") {
            authentication(userName: "${USER}", password: "${PASSWORD}")
        }
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "commons-io:commons-io:2.4"

    testCompile "junit:junit:4.11"

    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

release { tagPrefix = "${project.name}" }

createReleaseTag.dependsOn uploadArchives
